[doc Test REST API - GET]

[global baseUrl=http://jsonplaceholder.typicode.com]

[shell test]
	[global myUrl=$baseUrl/posts/2]
	[timeout 3]

	[progress 'GET - test response code 200']
	[invoke getHeader $myUrl]
	!echo Result: ==$responseCode==
	?==200==

	[progress 'GET - test attribute values']
	[invoke getBody $myUrl]
	!echo id: ==$id==
	?==2==
	!echo title: $title
	?"qui est esse"

	#expect response time for 5000 items less than 50ms
	[progress 'GET - test response time for 5000 items']
	[global photosUrl=$baseUrl/photos]
	[config multiplier=10]
	[timeout 5]
	[invoke getBody $photosUrl]
	!echo Result: ==$responseCode==
	?==200==

[cleanup]
	[progress 'Cleanup']

# http GET (headers only)
[macro getHeader url]
	[shell get]
	!curl -I $url
	?HTTP.+\s(\d+)
	[global responseCode=$1]
[endmacro]

# http GET body reponse
[macro getBody url]
	[shell get]
	!curl $url
	?"id":\s(.+),$
	[global id=$1]
	?"title":\s(.+),$
	[global title=$1]
[endmacro]
